{
	parserClass="com.windea.plugin.idea.pls.cwt.psi.CwtParser"
	extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
	psiClassPrefix="Cwt"
	psiImplClassSuffix="Impl"
	psiPackage="com.windea.plugin.idea.pls.cwt.psi"
	psiImplPackage="com.windea.plugin.idea.pls.cwt.psi.impl"
	elementTypeHolderClass="com.windea.plugin.idea.pls.cwt.psi.CwtTypes"
	tokenTypeClass="com.windea.plugin.idea.pls.cwt.psi.CwtTokenType"
	elementTypeClass="com.windea.plugin.idea.pls.cwt.psi.CwtElementType"
	psiImplUtilClass="com.windea.plugin.idea.pls.cwt.psi.impl.CwtPsiImplUtil"

	tokens=[
	  EQUAL_SIGN="="
	  LEFT_BRACE="{"
	  RIGHT_BRACE="}"
	  EOL="regexp:\R"
	  BLANK="regexp:\s+"
	  
	  COMMENT="regexp:#[^\r\n]*" //以"#"开始，单行
	  OPTION_COMMENT="regexp:##[^\r\n]*" //以"##"开始，单行
	  DOCUMENTATION_COMMENT="regexp:###[^\r\n]*" //以"###"开始，单行
	  
	  KEY_TOKEN='regexp:([^#={}\s][^={}\s]*)|("([^"\\\r\n]|\\.)*")'
	  BOOLEAN_TOKEN='regexp:(yes)|(no)'
	  INT_TOKEN='regexp:[+-]?(0|[1-9][0-9]*)'
	  FLOAT_TOKEN='regexp:[+-]?(0|[1-9][0-9]*)(\.[0-9]+)'
	  STRING_TOKEN='regexp:([^#={}\s"][^={}\s"]*)|("([^"\\\r\n]|\\.)*")'
	]
}

root ::= root_block
root_block ::= root_block_item * {
  extends=block
}
private root_block_item ::=  comment | property | value

private comment ::= COMMENT | OPTION_COMMENT | DOCUMENTATION_COMMENT

property ::= key separator value {
  pin=1
  mixin="com.windea.plugin.idea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements="com.windea.plugin.idea.pls.cwt.psi.CwtNamedElement"
  methods=[getIcon getName setName getPropertyKey getPropertyValue getPropertyTruncatedValue]
}

key ::= KEY_TOKEN{
  methods=[getName]
}

private separator ::= "="

value ::= boolean | number | string | block{
  methods=[getIcon getValue getTruncatedValue]
}

boolean ::= BOOLEAN_TOKEN {
  extends=value
}
number ::= int | float {
  extends=value
}
int ::= INT_TOKEN {
  extends=number
}
float ::= FLOAT_TOKEN{
  extends=number
}
string ::= STRING_TOKEN{
  extends=value
  methods=[getValue getTruncatedValue]
}
block ::= "{" block_item * "}" {
  pin=1
  extends=value
  implements="com.intellij.psi.PsiListLikeElement"
  methods=[isEmpty isNotEmpty isObject isArray getComponents]
}
private block_item ::= comment | property | value
