{
  parserClass="icu.windea.pls.localisation.parser.ParadoxLocalisationParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="ParadoxLocalisation"
  psiImplClassSuffix="Impl"
  psiPackage="icu.windea.pls.localisation.psi"
  psiImplPackage="icu.windea.pls.localisation.psi.impl"
  elementTypeHolderClass="icu.windea.pls.localisation.psi.ParadoxLocalisationElementTypes"
  tokenTypeFactory="icu.windea.pls.localisation.psi.ParadoxLocalisationElementTypeFactory.getTokenType"
  elementTypeFactory="icu.windea.pls.localisation.psi.ParadoxLocalisationElementTypeFactory.getElementType"
  psiImplUtilClass="icu.windea.pls.localisation.psi.impl.ParadoxLocalisationPsiImplUtil"
  parserUtilClass="icu.windea.pls.localisation.parser.ParadoxLocalisationParserUtil"
  methods(".*")=[getPresentation getResolveScope getUseScope]
}

// core rules

root ::= property_list *

property_list ::= COMMENT * (property_item_list_with_locale | property_item_list) {
  implements="com.intellij.psi.PsiListLikeElement"
  methods=[getIcon getComponents]
}
private property_item_list_with_locale ::= locale property_item * {
  pin=1
}
private property_item_list ::= property_item +
private property_item ::= COMMENT | property

locale ::= LOCALE_TOKEN COLON {
  pin=1
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
  ]
  methods=[getIdElement getIcon getName setName getReference getReferences]
}

property ::= property_key COLON property_number? property_value {
  pin=1
  recoverWhile="#auto"
  extends="icu.windea.pls.localisation.psi.impl.ParadoxLocalisationStubElementImpl<?>"
  stubClass="icu.windea.pls.localisation.psi.ParadoxLocalisationPropertyStub"
  implements="icu.windea.pls.localisation.psi.ParadoxLocalisationNamedElement"
  methods=[
    getIcon getName setName getNameIdentifier getTextOffset getCategory getValue setValue
    isEquivalentTo toString
  ]
}

property_key ::= PROPERTY_KEY_TOKEN {
  methods=[getIdElement getIcon]
}

// The number seen in vanilla files right after the colon can be omitted, as it is only useful for Paradoxâ€™s internal translation tracking
// https://stellaris.paradoxwikis.com/Localisation_modding
// https://twitter.com/Martin_Anward/status/1039175213773144066
private property_number ::= PROPERTY_NUMBER

property_value ::= LEFT_QUOTE PROPERTY_VALUE_TOKEN ? RIGHT_QUOTE { // lazy-parsing PROPERTY_VALUE_TOKEN
  pin=1
  methods=[getTokenElement getRichTextList]
}

// ------

// localisation text rules (lazy-parsing, from PROPERTY_VALUE_TOKEN)

text_root ::= text_items {
  extraRoot=true
}
private text_items ::= text_item +
private text_item ::= rich_text | COLORFUL_TEXT_END | TEXT_FORMAT_END

rich_text ::= string | colorful_text | parameter | icon | command | concept_command | text_format | text_icon

string ::= STRING_TOKEN {
  extends=rich_text
  methods=[getIdElement]
}

// localisation colorful text rules

// see: https://stellaris.paradoxwikis.com/Localisation_modding#Color_Codes
colorful_text ::= COLORFUL_TEXT_START color_name colorful_text_part ? COLORFUL_TEXT_END ? {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationTextColorAwareElement"
  ]
  methods=[getIdElement getName setName getReference getReferences]
}
private color_name ::= COLOR_TOKEN
private colorful_text_part ::= colorful_text_item +
private colorful_text_item ::= rich_text | TEXT_FORMAT_END

// localisation reference rules

// see: https://stellaris.paradoxwikis.com/Localisation_modding#.24_Codes
// may be resolved to localisation / parameter / system statistics in GUI elements
parameter ::= PARAMETER_START parameter_name parameter_argument_part ? PARAMETER_END {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationArgumentAwareElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationCommandAwareElement"
  ]
  methods=[
    getIdElement getArgumentElement parameter_argument="" command=""
    getName setName getReference getReferences
  ]
}
// can be command or scripted variable reference
private parameter_name ::= PARAMETER_TOKEN | command | scripted_variable_reference
private parameter_argument_part ::= PIPE parameter_argument ?
parameter_argument ::= ARGUMENT_TOKEN {
  implements=[
    "icu.windea.pls.localisation.psi.ParadoxLocalisationArgument"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationTextColorAwareElement"
  ]
  methods=[getIdElement getReference getReferences]
}

// scripted variable reference rules (in references)

scripted_variable_reference ::= AT SCRIPTED_VARIABLE_REFERENCE_TOKEN {
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.lang.psi.ParadoxScriptedVariableReference"
  ]
  methods=[getIdElement getIcon getName setName getReference getReferences]
}

// localisation command rules

// see: https://stellaris.paradoxwikis.com/Localisation_modding#Bracket_Commands
command ::= <<isCommand>> LEFT_BRACKET command_text ? command_argument_part ? RIGHT_BRACKET {
  pin=2
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationArgumentAwareElement"
  ]
  methods=[
    getArgumentElement command_argument=""
    getIcon
  ]
}
// can be parameterized
command_text ::= (COMMAND_TEXT_TOKEN | parameter) + {
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationExpressionElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
  ]
  methods=[
    getIdElement parameter=""
    getName getValue setValue getReference getReferences
  ]
}
private command_argument_part ::= PIPE command_argument ?
// can be parameterized
command_argument ::= (ARGUMENT_TOKEN | parameter) + {
  implements=[
    "icu.windea.pls.localisation.psi.ParadoxLocalisationArgument"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationTextColorAwareElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
  ]
  methods=[getIdElement parameter="" getReference getReferences]
}

// localisation icon rules

icon ::= ICON_START icon_name icon_argument_part ? ICON_END {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationArgumentAwareElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationCommandAwareElement"
  ]
  methods=[
    getIdElement getArgumentElement icon_argument="" rich_text=""
    getIcon getName setName getFrame getReference getReferences
  ]
}
// can be parameterized or command
private icon_name ::= (ICON_TOKEN | parameter) + | command
private icon_argument_part ::= PIPE icon_argument ?
// can be parameterized
icon_argument ::= (ARGUMENT_TOKEN | parameter) + {
  implements=[
    "icu.windea.pls.localisation.psi.ParadoxLocalisationArgument"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
  ]
  methods=[getIdElement parameter=""]
}

// [stellaris] localisation concept command rules (as special command rules)

concept_command ::= <<isConceptCommand>> LEFT_BRACKET LEFT_SINGLE_QUOTE concept_name ? RIGHT_SINGLE_QUOTE concept_text_part ? RIGHT_BRACKET {
  pin=2
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
  ]
  methods=[getIcon getName setName getReference getReferences]
}
// can be parameterized
concept_name ::= (CONCEPT_NAME_TOKEN | parameter) + {
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationExpressionElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
  ]
  methods=[
    getIdElement parameter=""
    getIcon getName getValue setValue getReference getReferences
  ]
}
private concept_text_part ::= COMMA concept_text ?
concept_text ::= concept_text_item +
private concept_text_item ::= rich_text | COLORFUL_TEXT_END | TEXT_FORMAT_END

// [ck3, vic3] localisation text format rules

text_format ::= TEXT_FORMAT_START text_format_name text_format_text_part ? TEXT_FORMAT_END ? {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationCommandAwareElement"
  ]
  methods=[
    getIdElement rich_text=""
    getIcon getName setName getReference getReferences
  ]
}
// can be parameterized or command
private text_format_name ::= (TEXT_FORMAT_TOKEN | parameter) + | command
private text_format_text_part ::= text_format_text
text_format_text ::= text_format_text_item +
private text_format_text_item ::= rich_text | COLORFUL_TEXT_END

// [ck3, vic3] localisation text icon rules

text_icon ::= TEXT_ICON_START text_icon_name TEXT_ICON_END {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationParameterAwareElement"
    "icu.windea.pls.localisation.psi.ParadoxLocalisationCommandAwareElement"
  ]
  methods=[
    getIdElement rich_text=""
    getIcon getName setName getReference getReferences
  ]
}
// can be parameterized or command
private text_icon_name ::= (TEXT_ICON_TOKEN | parameter) + | command
