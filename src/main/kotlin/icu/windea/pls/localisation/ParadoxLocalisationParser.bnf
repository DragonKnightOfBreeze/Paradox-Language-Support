//Stellaris官方本地化文件中本身就存在语法解析错误，需要保证存在错误的情况下仍然会解析后续的本地化文本，草

{
  parserClass="icu.windea.pls.localisation.psi.ParadoxLocalisationParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="ParadoxLocalisation"
  psiImplClassSuffix="Impl"
  psiPackage="icu.windea.pls.localisation.psi"
  psiImplPackage="icu.windea.pls.localisation.psi.impl"
  elementTypeHolderClass="icu.windea.pls.localisation.psi.ParadoxLocalisationElementTypes"
  elementTypeClass="icu.windea.pls.localisation.psi.ParadoxLocalisationElementType"
  tokenTypeClass="icu.windea.pls.localisation.psi.ParadoxLocalisationTokenType"
  elementTypeFactory="icu.windea.pls.localisation.psi.ParadoxLocalisationElementTypeFactory.getElementType"
  tokenTypeFactory="icu.windea.pls.localisation.psi.ParadoxLocalisationElementTypeFactory.getTokenType"
  psiImplUtilClass="icu.windea.pls.localisation.psi.impl.ParadoxLocalisationPsiImplUtil"
  parserUtilClass="icu.windea.pls.localisation.psi.ParadoxLocalisationParserUtil"
  tokens=[
    COLON=":"
    AT="@"
    PIPE="|"
  ]
  methods(".*")=[getPresentation getResolveScope getUseScope]
}

//规则定义可以忽略空白
//直接导向token的rule，在flex文件中需要使用对应的token
//即使idea提示不需要转义，一些字符在regexp中仍然需要转义

//core rules

root ::= property_list *
//locale之前可以添加注释，wiki中的说明有误
property_list ::= COMMENT * (property_item_list_with_locale | property_item_list) {
  implements="com.intellij.psi.PsiListLikeElement"
  methods=[getIcon getComponents]
}
private property_item_list_with_locale ::= locale property_item * {
  pin=1
}
private property_item_list ::= property_item +
private property_item ::= COMMENT | property {
  recoverWhile="#auto"
}

locale ::= LOCALE_TOKEN COLON {
  pin=1
  recoverWhile="#auto"
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
  ]
  methods=[getIcon getName setName getReference]
}

property ::= property_key COLON property_number? property_value {
  pin=1
  recoverWhile="#auto" 
  elementTypeFactory="icu.windea.pls.localisation.psi.ParadoxLocalisationStubElementTypes.getPropertyType"
  extends="icu.windea.pls.localisation.psi.impl.ParadoxLocalisationStubElementImpl<?>"
  stubClass="icu.windea.pls.localisation.psi.ParadoxLocalisationPropertyStub"
  implements="icu.windea.pls.localisation.psi.ParadoxLocalisationNamedElement"
  methods=[
    getIcon getName setName getNameIdentifier getTextOffset getCategory getValue setValue
    isEquivalentTo toString
  ]
}
//https://stellaris.paradoxwikis.com/Localisation_modding
//The number seen in vanilla files right after the colon can be omitted, as it is only useful for Paradox’s internal translation tracking
//https://twitter.com/Martin_Anward/status/1039175213773144066
private property_number ::= PROPERTY_NUMBER
property_key ::= PROPERTY_KEY_TOKEN {
  methods=[getIcon]
}
property_value ::= LEFT_QUOTE property_value_item RIGHT_QUOTE{
  pin=1
}

//允许多余的彩色文本结束标记（"§!"）
private property_value_item ::= (rich_text | COLORFUL_TEXT_END) *
rich_text ::= escape
  | property_reference
  | icon
  | colorful_text
  | command
  | string

string ::= STRING_TOKEN{
  extends=rich_text
}

//escape rules

escape ::= VALID_ESCAPE_TOKEN | INVALID_ESCAPE_TOKEN | DOUBLE_LEFT_BRACKET{
  extends=rich_text
}

//reference rules

//see: https://stellaris.paradoxwikis.com/Localisation_modding#.24_Codes
//may be resolved to localisation / parameter / system statistics in GUI elements 
property_reference ::= PROPERTY_REFERENCE_START [property_reference_name] [PIPE [property_reference_parameter]] PROPERTY_REFERENCE_END {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
  ]
  methods=[getName setName getReference]
}
//token / command (e.g. "[Root.GetName]") / scripted_variable (e.g. "@v") 
private property_reference_name ::= PROPERTY_REFERENCE_TOKEN | command | scripted_variable_reference
private property_reference_parameter ::= PROPERTY_REFERENCE_PARAMETER_TOKEN //TODO check syntax

//scripted variable reference rules

//scripted variable reference
scripted_variable_reference ::= AT SCRIPTED_VARIABLE_REFERENCE_TOKEN {
  implements=[
    "icu.windea.pls.core.psi.ParadoxScriptedVariableReference"
  ]
  methods=[getIcon getName setName getReference getType getExpression]
}

//icon rules

//see: https://stellaris.paradoxwikis.com/Localisation_modding#.C2.A3_Codes
icon ::= ICON_START icon_name [PIPE icon_frame] ICON_END {
  pin=1
  extends=rich_text
  implements=[
    "com.intellij.psi.NavigatablePsiElement"  
  ]
  methods=[getIcon getName setName getFrame getReference]
}
private icon_name ::= command | property_reference | ICON_TOKEN //can be command / property reference
private icon_frame ::= ICON_FRAME | property_reference //can be property reference (resolved as parameter)

//command rules

//see: https://stellaris.paradoxwikis.com/Localisation_modding#Bracket_Commands
command ::= COMMAND_START command_expression? COMMAND_END{
  pin=1
  extends=rich_text
  implements="icu.windea.pls.localisation.psi.ParadoxLocalisationRichText"
  methods=[getIcon]
}
private command_expression ::= concept_expression | command_invocation_expression {
  pin=1
}
private command_invocation_expression ::= (command_scope DOT)* command_field {
  pin=1
}
//noinspection BnfUnusedRule
command_identifier ::= {
  implements="icu.windea.pls.core.psi.ParadoxTypedElement"
  implements=[
    "com.intellij.psi.NavigatablePsiElement"  
  ]
  methods=[getPrevIdentifier getNextIdentifier]
}
command_scope ::= COMMAND_SCOPE_TOKEN {
  extends=command_identifier
  methods=[getIcon getName setName getReference getPrevIdentifier getNextIdentifier getExpression getConfigExpression]
}
command_field ::= COMMAND_FIELD_TOKEN | property_reference { //can be property reference (resolved as parameter)
  extends=command_identifier
  methods=[getIcon getName setName getReference getPrevIdentifier getNextIdentifier getExpression getConfigExpression]
}
private concept_expression ::= concept_name (COMMA concept_text) ? {
  pin=1
}
concept_name ::= LEFT_SINGLE_QUOTE CONCEPT_NAME_TOKEN RIGHT_SINGLE_QUOTE {
  pin=1
  methods=[getIcon getName setName getReference]
}
concept_text ::= rich_text *

//colorful text rules

//see: https://stellaris.paradoxwikis.com/Localisation_modding#Color_Codes
colorful_text ::= COLORFUL_TEXT_START COLOR_TOKEN colorful_text_item [COLORFUL_TEXT_END]{
  pin=1
  extends=rich_text
  methods=[getName setName getReference]
}
private colorful_text_item ::= rich_text *
