{
  parserImports="static com.intellij.lang.WhitespacesBinders.*"
  parserClass="icu.windea.pls.csv.parser.ParadoxCsvParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="ParadoxCsv"
  psiImplClassSuffix="Impl"
  psiPackage="icu.windea.pls.csv.psi"
  psiImplPackage="icu.windea.pls.csv.psi.impl"
  elementTypeHolderClass="icu.windea.pls.csv.psi.ParadoxCsvElementTypes"
  elementTypeFactory="icu.windea.pls.csv.psi.ParadoxCsvElementTypeFactory.getElementType"
  tokenTypeFactory="icu.windea.pls.csv.psi.ParadoxCsvElementTypeFactory.getTokenType"
  psiImplUtilClass="icu.windea.pls.csv.psi.impl.ParadoxCsvPsiImplUtil"
  parserUtilClass="icu.windea.pls.csv.parser.ParadoxCsvParserUtil"
  tokens=[
    SEPARATOR=";"
    EOL="<eol>"
  ]
  methods(".*")=[getPresentation getResolveScope getUseScope]
}

root ::= COMMENT * header ? (row | COMMENT) *

header ::= row_item {
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "com.intellij.psi.PsiListLikeElement"
  ]
  methods=[getIcon getComponents toString]
}
row ::= row_item {
  extends="icu.windea.pls.csv.psi.impl.ParadoxCsvStubElementImpl<?>"
  stubClass="icu.windea.pls.csv.psi.ParadoxCsvRowStub"
  implements=[
    "com.intellij.psi.NavigatablePsiElement"
    "com.intellij.psi.PsiListLikeElement"
  ]
  methods=[getIcon getComponents toString]
}
private row_item ::= column column_item_with_separator * {
  pin=1
  recoverWhile="#auto"
}
private column_item_with_separator ::= <<checkLineBreak>> SEPARATOR column_item ?
private column_item ::= <<checkLineBreak>> column
column ::= COLUMN_TOKEN {
  implements=[
    "com.intellij.psi.PsiLiteralValue"
    "icu.windea.pls.csv.psi.ParadoxCsvExpressionElement"
  ]
  methods=[getIcon getName getValue setValue toString]
}
