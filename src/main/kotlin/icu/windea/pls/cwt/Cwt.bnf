{
  parserClass="icu.windea.pls.cwt.parser.CwtParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="Cwt"
  psiImplClassSuffix="Impl"
  psiPackage="icu.windea.pls.cwt.psi"
  psiImplPackage="icu.windea.pls.cwt.psi.impl"
  elementTypeHolderClass="icu.windea.pls.cwt.psi.CwtElementTypes"
  tokenTypeFactory="icu.windea.pls.cwt.psi.CwtElementTypeFactory.getTokenType"
  elementTypeFactory="icu.windea.pls.cwt.psi.CwtElementTypeFactory.getElementType"
  psiImplUtilClass="icu.windea.pls.cwt.psi.impl.CwtPsiImplUtil"
  parserUtilClass="icu.windea.pls.cwt.parser.CwtParserUtil"
  methods(".*")=[getPresentation getUseScope]
}

// core rules

root ::= root_block ?

root_block ::= root_block_item + {
  implements=["icu.windea.pls.cwt.psi.CwtBlockElement"]
  methods=[getValue isEmpty isNotEmpty getComponents]
}
private root_block_item ::= general_comment | property | value {
  recoverWhile="#auto"
}

private general_comment ::= doc_comment | option_comment | comment
doc_comment ::= DOC_COMMENT_TOKEN {
  implements=["com.intellij.psi.PsiComment"]
  methods=[getTokenType]
}
option_comment ::= OPTION_COMMENT_TOKEN { // lazy-parsing OPTION_COMMENT_TOKEN
  implements=["com.intellij.psi.PsiComment"]
  methods=[getTokenType getTokenElement getOption getOptionValue]
}
private comment ::= COMMENT

property ::= property_key property_separator property_value {
  pin=1
  recoverWhile="#auto"
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtMemberElement"
  ]
  methods=[
    propertyKey="property_key" propertyValue="value"
    getIcon getName setName getNameIdentifier getValue getSeparatorType
  ]
}
property_key ::= PROPERTY_KEY_TOKEN {
  implements=[
    "com.intellij.psi.PsiLiteralValue"
    "icu.windea.pls.cwt.psi.CwtStringExpressionElement"
  ]
  methods=[getIcon getName getValue setValue getReference getReferences]
}
private property_separator ::= EQUAL_SIGN | NOT_EQUAL_SIGN
private property_value ::= value

value ::= boolean | int | float | string | block {
  implements=[
    "icu.windea.pls.cwt.psi.CwtExpressionElement"
    "icu.windea.pls.cwt.psi.CwtMemberElement"
    "icu.windea.pls.cwt.psi.CwtOptionMemberElement"
  ]
  methods=[getIcon getName getValue setValue]
}
boolean ::= BOOLEAN_TOKEN {
  extends=value
  implements=[
    "com.intellij.psi.PsiLiteralValue"
  ]
  methods=[getBooleanValue]
}
int ::= INT_TOKEN {
  extends=value
  implements=[
    "com.intellij.psi.PsiLiteralValue"
  ]
  methods=[getIntValue]
}
float ::= FLOAT_TOKEN {
  extends=value
  implements=[
    "com.intellij.psi.PsiLiteralValue"
  ]
  methods=[getFloatValue]
}
string ::= STRING_TOKEN {
  extends=value
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "com.intellij.psi.PsiLiteralValue"
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtStringExpressionElement"
  ]
  methods=[getIcon getName setName getNameIdentifier getValue setValue getStringValue getReference getReferences]
}
block ::= LEFT_BRACE block_item * RIGHT_BRACE {
  pin=1
  extends=value
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtBlockElement"
  ]
  methods=[getIcon getName setName getValue setValue isEmpty isNotEmpty getComponents]
}
private block_item ::= general_comment | property | option | value {
  recoverWhile="#auto"
}

// ------

// option comment rules (lazy-parsing, from OPTION_COMMENT_TOKEN)

option_comment_root ::= OPTION_COMMENT_START option_comment_item ? comment ? {
  extraRoot=true
  methods=[option="option" option_value="value"]
}
private option_comment_item ::= option | option_value

option ::= option_key option_separator option_value {
  pin=1
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtOptionMemberElement"
  ]
  methods=[
    option_key="option_key" option_value="value"
    getIcon getName setName getNameIdentifier getValue getSeparatorType
  ]
}
option_key ::= OPTION_KEY_TOKEN {
  methods=[getIcon getName getValue]
}
private option_separator ::= EQUAL_SIGN | NOT_EQUAL_SIGN
private option_value ::= value
