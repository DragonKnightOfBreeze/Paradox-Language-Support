{
  parserClass="icu.windea.pls.cwt.psi.CwtParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  psiClassPrefix="Cwt"
  psiImplClassSuffix="Impl"
  psiPackage="icu.windea.pls.cwt.psi"
  psiImplPackage="icu.windea.pls.cwt.psi.impl"
  elementTypeHolderClass="icu.windea.pls.cwt.psi.CwtElementTypes"
  elementTypeClass="icu.windea.pls.cwt.psi.CwtElementType"
  tokenTypeClass="icu.windea.pls.cwt.psi.CwtTokenType"
  psiImplUtilClass="icu.windea.pls.cwt.psi.util.CwtPsiImplUtil"
  psiVisitorName=""
  methods(".*")=[getPresentation getUseScope]
}

root ::= root_block ?

root_block ::= root_block_item + {
  implements=["icu.windea.pls.cwt.psi.CwtBlockElement"]
  methods=[getValue isEmpty isNotEmpty getComponents]
}
private root_block_item ::= general_comment | property | value {
  recoverWhile="#auto"
}

private general_comment ::= doc_comment | option_comment | comment
doc_comment ::= DOC_COMMENT_TOKEN {
  implements=["com.intellij.psi.PsiComment"]
  methods=[getTokenType]
}
option_comment ::= OPTION_COMMENT_TOKEN {
  implements=["com.intellij.psi.PsiComment"]
  methods=[getTokenType getOption getOptionValue]
  elementTypeClass="icu.windea.pls.cwt.psi.CwtOptionCommentElementType"
}
private comment ::= COMMENT

property ::= property_key property_separator value {
  pin=1
  recoverWhile="#auto"
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtMemberElement"
  ]
  methods=[
    propertyKey="property_key" propertyValue="value"
    getIcon getName setName getNameIdentifier getValue getSeparatorType
  ]
}
property_key ::= PROPERTY_KEY_TOKEN {
  methods=[getIcon getName getValue setValue getType getConfigType]
  implements=[
    "com.intellij.psi.PsiLiteralValue"
    "icu.windea.pls.cwt.psi.CwtStringExpressionElement"
  ]
}
private property_separator ::= EQUAL_SIGN | NOT_EQUAL_SIGN

value ::= boolean | int | float | string | block {
  implements=[
    "icu.windea.pls.cwt.psi.CwtExpressionElement"
    "icu.windea.pls.cwt.psi.CwtMemberElement"
  ]
  methods=[getIcon getName getValue setValue getType getConfigType]
}
boolean ::= BOOLEAN_TOKEN {
  extends=value
  implements=[
    "com.intellij.psi.PsiLiteralValue"
  ]
  methods=[getBooleanValue getType getConfigType]
}
int ::= INT_TOKEN {
  extends=value
  implements=[
    "com.intellij.psi.PsiLiteralValue"
  ]
  methods=[getIntValue getType getConfigType]
}
float ::= FLOAT_TOKEN {
  extends=value
  implements=[
    "com.intellij.psi.PsiLiteralValue"
  ]
  methods=[getFloatValue getType getConfigType]
}
string ::= STRING_TOKEN {
  extends=value
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "com.intellij.psi.PsiLiteralValue"
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtStringExpressionElement"
  ]
  methods=[getIcon getName setName getNameIdentifier getValue setValue getStringValue getType getConfigType]
}
block ::= LEFT_BRACE block_item * RIGHT_BRACE {
  pin=1
  extends=value
  mixin="icu.windea.pls.cwt.psi.impl.CwtNamedElementImpl"
  implements=[
    "icu.windea.pls.cwt.psi.CwtNamedElement"
    "icu.windea.pls.cwt.psi.CwtBlockElement"
  ]
  methods=[getIcon getName setName getValue setValue isEmpty isNotEmpty getComponents getType getConfigType]
}
private block_item ::= comment | property | value {
  recoverWhile="#auto"
}
