{
	parserClass="icu.windea.pls.script.psi.ParadoxScriptParser"
	extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
	psiClassPrefix="ParadoxScript"
	psiImplClassSuffix="Impl"
	psiPackage="icu.windea.pls.script.psi"
	psiImplPackage="icu.windea.pls.script.psi.impl"
	elementTypeHolderClass="icu.windea.pls.script.psi.ParadoxScriptTypes"
	tokenTypeClass="icu.windea.pls.script.psi.ParadoxScriptTokenType"
	elementTypeClass="icu.windea.pls.script.psi.ParadoxScriptElementType"
	psiImplUtilClass="icu.windea.pls.script.psi.impl.ParadoxScriptPsiImplUtil"

	tokens=[
	  BLANK='regexp:\s+'
	  NEW_LINE='regexp:\R'
	  EQUAL_SIGN="="
	  LT_SIGN="<"
	  GT_SIGN=">"
	  LE_SIGN="<="
	  GE_SIGN=">="
	  NOT_EQUAL_SIGN="<>"
	  //0123.45
	  LEFT_BRACE="{"
	  RIGHT_BRACE="}"
	  CODE_START="@\["
	  CODE_END="]"
	  EOL="regexp:\R"
	  BLANK="regexp:\s+"
	  COMMENT="regexp:#[^\r\n]*"
	  END_OF_LINE_COMMENT="regexp:#[^\r\n]*"
	  VARIABLE_NAME_ID="regexp:@[a-zA-Z0-9_-]+"
	  VARIABLE_REFERENCE_ID="regexp:@[a-zA-Z0-9_-]+"
	  PROPERTY_KEY_ID='regexp:[^#@=\s{}]+[^=\s{}]*'
	  QUOTED_PROPERTY_KEY_ID='regexp:"([^"(\r\n\\]|\\.)*?"'
	  BOOLEAN_TOKEN='regexp:(yes)|(no)'
	  INT_TOKEN='regexp:[+-]?(0|[1-9][0-9]*)'
	  FLOAT_TOKEN='regexp:[+-]?(0|[1-9][0-9]*)(\.[0-9]+)'
	  STRING_TOKEN='regexp:[^@\s{}="][^\s{}="]*'
	  QUOTED_STRING_TOKEN='regexp:"([^"\r\n\\]|\\.)*?"'
	  COLOR_TOKEN="regexp:(rgb|rgba|hsb|hsv|hsl)[ \t]*\{[\d. \t]*}"
      CODE_TEXT_TOKEN="regexp:[^\r\n\]}]+"
	]
}

//忽略空白，在ParserDefinition里面定义PsiElement之间的空白类型
//regex必须定向到token，而非rule
//即使idea提示不需要转义，一些字符在regexp中仍然需要转义

//DONE 列表的元素可以是字符串
//DONE 分隔符可以是比较运算符

//root_block也是block，但不能直接extends，否则root下面会是value而非root_block
root ::= root_block
root_block ::= root_block_item * {
  extends=block
}
private root_block_item ::=  END_OF_LINE_COMMENT | COMMENT | variable | property | value

variable ::= variable_name variable_separator variable_value {
  pin(".*")=1
  recoverWhile=variable_recover
  elementTypeFactory="icu.windea.pls.script.psi.ParadoxScriptStubElementTypes.getVariableType"
  extends="icu.windea.pls.script.psi.impl.ParadoxScriptStubElementImpl<?>"
  implements="icu.windea.pls.script.psi.ParadoxScriptNamedElement"
  stubClass="icu.windea.pls.script.psi.ParadoxScriptVariableStub"
  methods=[getName setName getNameIdentifier getIcon getValue getUnquotedValue]
}

variable_name ::= VARIABLE_NAME_ID{
  methods=[getValue setValue]
}
private variable_separator ::= "="
variable_value ::= boolean | number | string
//当前变量解析失败时，需要跳过继续解析下一行
private variable_recover ::= !(NEW_LINE | '{' | '}' | BOOLEAN_TOKEN | CODE_START | COLOR_TOKEN | COMMENT | END_OF_LINE_COMMENT | FLOAT_TOKEN | INT_TOKEN | PROPERTY_KEY_ID | QUOTED_PROPERTY_KEY_ID | QUOTED_STRING_TOKEN | STRING_TOKEN | VARIABLE_NAME_ID | VARIABLE_REFERENCE_ID)

property ::= property_key property_separator property_value {
  pin(".*")=1
  recoverWhile=property_recover
  elementTypeFactory="icu.windea.pls.script.psi.ParadoxScriptStubElementTypes.getPropertyType"
  extends="icu.windea.pls.script.psi.impl.ParadoxScriptStubElementImpl<?>"
  implements=[
    "icu.windea.pls.script.psi.ParadoxScriptNamedElement"
    "icu.windea.pls.script.psi.ParadoxDefinitionProperty"
  ]
  stubClass="icu.windea.pls.script.psi.ParadoxScriptDefinitionStub"
  methods=[getName setName getNameIdentifier getIcon getValue getTruncatedValue getDepth getBlock]
}
property_key ::= PROPERTY_KEY_ID | QUOTED_PROPERTY_KEY_ID{
  methods=[getValue setValue getReference]
}
private property_separator ::= "=" | "<" | ">" | "<=" | ">=" | "<>"
property_value ::= value
//当前属性解析失败时，需要跳过继续解析下一个
private property_recover ::= !('{' | '}' | BOOLEAN_TOKEN | CODE_START | COLOR_TOKEN | COMMENT | END_OF_LINE_COMMENT | FLOAT_TOKEN | INT_TOKEN | PROPERTY_KEY_ID | QUOTED_PROPERTY_KEY_ID | QUOTED_STRING_TOKEN | STRING_TOKEN | VARIABLE_NAME_ID | VARIABLE_REFERENCE_ID)

value ::= variable_reference | boolean | number | string_value | block{
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getIcon getValue getTruncatedValue]
}
variable_reference ::= VARIABLE_REFERENCE_ID {
  extends=value
  methods=[getName setName getReference getReferenceValue]
}
boolean ::= BOOLEAN_TOKEN {
  extends=value
  methods=[getBooleanValue]
}
number ::= int | float {
  extends=value
}
int ::= INT_TOKEN {
  extends=number
  methods=[getIntValue]
}
float ::= FLOAT_TOKEN{
  extends=number
  methods=[getFloatValue]
}
string_value ::= color | code | string{
  extends=value
}
string ::= STRING_TOKEN | QUOTED_STRING_TOKEN {
  extends=string_value
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue setValue getReference getStringValue]
}
color ::= COLOR_TOKEN {
  extends=string_value
  implements="com.intellij.psi.PsiLiteralValue"
  methods=[getValue getColor setColor]
}
code ::= CODE_START code_text CODE_END{
  pin(".*")=1
  extends=string_value
}
private code_text ::= CODE_TEXT_TOKEN
block ::= "{" block_item * "}" {
  pin(".*")=1
  extends=value
  implements="com.intellij.psi.PsiListLikeElement"
  methods=[getValue getTruncatedValue isEmpty isNotEmpty isObject isArray getComponents]
}
private block_item ::= END_OF_LINE_COMMENT | COMMENT | property | value
